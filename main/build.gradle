plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'jacoco'
}

group = 'com.amorgakco'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    asciidoctorExt
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testRuntimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    implementation 'net.nurigo:sdk:4.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'com.google.firebase:firebase-admin:9.3.0'
    implementation 'com.google.geometry:s2-geometry:2.0.0'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
    runtimeOnly 'com.h2database:h2'
    testImplementation "org.junit.jupiter:junit-jupiter:5.8.1"
    testImplementation "org.testcontainers:junit-jupiter:1.16.3"
    testImplementation "com.redis:testcontainers-redis:2.0.1"
    testImplementation "org.testcontainers:mysql:1.17.2"
    implementation "org.springframework.retry:spring-retry"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
}

tasks.named('test') {
    useJUnitPlatform()
    doFirst { delete(snippetsDir) }
    outputs.dir snippetsDir
    finalizedBy 'jacocoTestReport'
    finalizedBy 'asciidoctor'
}

// asciidoc setting

ext {
    set('snippetsDir', file("build/generated-snippets"))
    set('docsOutputDir', file("build/docs/asciidoc"))
    set('projectDocsDir', file("main/src/main/resources/static/docs"))
    set('deployDocsDir', file("../docs"))
}

tasks.named('asciidoctor') {
    configurations 'asciidoctorExt'
    doFirst {
        delete(docsOutputDir)
        delete(projectDocsDir)
        delete(deployDocsDir)
    }
    sources {
        include("**/index.adoc")
    }
    baseDirFollowsSourceFile()
    inputs.dir snippetsDir
    dependsOn test
    doLast {
        copy {
            from docsOutputDir
            into deployDocsDir
        }
    }
}

// asciidoc setting end

// jacoco setting
jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
        csv.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ["com/amorgakco/backend/BackendApplication.class",
                                        "com/amorgakco/backend/global/config/**",
                                        "com/amorgakco/backend/global/exception/**",
                                        "com/amorgakco/backend/**/dto/**",
                                        "com/amorgakco/backend/**/mapper/**",
                                        "com/amorgakco/backend/global/oauth/**",
                                        "com/amorgakco/backend/global/response/**",
                                        "com/amorgakco/backend/global/properties/**",
                                        "com/amorgakco/backend/global/rabbitmq/**",
                                        "com/amorgakco/backend/oauth2/provider/**",
                                        "com/amorgakco/backend/oauth2/service/**",
            ])
        }))
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 0.00
            }

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.00
            }
        }
    }
}

bootRun{
    jvmArgs([
            "-Dcom.sun.management.jmxremote=true",
"-Dcom.sun.management.jmxremote.local.only=false",
"-Dcom.sun.management.jmxremote.port=9010",
"-Dcom.sun.management.jmxremote.ssl=false",
"-Dcom.sun.management.jmxremote.authenticate=false",
"-Djava.rmi.server.hostname=34.64.170.190",
"-Dcom.sun.management.jmxremote.rmi.port=9010"
    ])
}